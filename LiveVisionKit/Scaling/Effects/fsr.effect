#include "ffx_a_mod.h"

//=====================================================================================
//		OBS Parameters & Callbacks
//=====================================================================================

uniform float4x4 ViewProj;
uniform texture2d image; 

//=====================================================================================
//		FSR Parameters & Callbacks
//=====================================================================================

uniform AF2 output_size;
uniform AF4 easu_const_0;
uniform AF4 easu_const_1;
uniform AF4 easu_const_2;
uniform AF4 easu_const_3;
uniform AF4 rcas_const_0;

sampler_state LinearSampler 
{
    Filter      = Linear; 
    AddressU    = Clamp;
    AddressV    = Clamp;
};

sampler_state PointSampler 
{
    Filter      = Point; 
    AddressU    = Clamp;
    AddressV    = Clamp;
};

AF4 FsrEasuRF(AF2 p)
{
#ifdef GLSL
    // For GLSL we should use the textureGather function here, however that 
    // required GLSL 400+ while we are stuck with 330 for OBS. So we instead
    // mimic the functionality by using texel fetch.  
    //
    // For a given point p in normalized texture coordinates. We want to grab neighbouring
    // texels x, y, z, and w in the listed order.
    //
    //                                  +---+---+
    //                                  | w | z |
    //                                  +--(p)--+
    //                                  | x | y |
    //                                  +---+---+
    //
    // The size of a texel is pre-computed by the FsrEasuCon call and stored within the .xy
    // of con1. This is called on the CPU side and passed in via uniform to easu_const_1. 
    // To get the normalized texture coordinates for each required texel, we then offset 
    // point p by half the texel size using addition/subtraction as necessary.
    AF2 half_texel_size = AF2_(0.5) * easu_const_1.xy;
    return AF4(
        image.Sample(PointSampler, AF2(p.x - half_texel_size.x, p.y + half_texel_size.y)).r,
        image.Sample(PointSampler, AF2(p.x + half_texel_size.x, p.y + half_texel_size.y)).r,
        image.Sample(PointSampler, AF2(p.x + half_texel_size.x, p.y - half_texel_size.y)).r,
        image.Sample(PointSampler, AF2(p.x - half_texel_size.x, p.y - half_texel_size.y)).r
    );
#else
    return GatherRed(LinearSampler, p, ASU2(0, 0));
#endif  
}

AF4 FsrEasuGF(AF2 p)
{
#ifdef GLSL
    // For GLSL we should use the textureGather function here, however that 
    // required GLSL 400+ while we are stuck with 330 for OBS. So we instead
    // mimic the functionality by using texel fetch.  
    //
    // For a given point p in normalized texture coordinates. We want to grab neighbouring
    // texels x, y, z, and w in the listed order.
    //
    //                                  +---+---+
    //                                  | w | z |
    //                                  +--(p)--+
    //                                  | x | y |
    //                                  +---+---+
    //
    // The size of a texel is pre-computed by the FsrEasuCon call and stored within the .xy
    // of con1. This is called on the CPU side and passed in via uniform to easu_const_1. 
    // To get the normalized texture coordinates for each required texel, we then offset 
    // point p by half the texel size using addition/subtraction as necessary.
    AF2 half_texel_size = AF2_(0.5) * easu_const_1.xy;
    return AF4(
        image.Sample(PointSampler, AF2(p.x - half_texel_size.x, p.y + half_texel_size.y)).g,
        image.Sample(PointSampler, AF2(p.x + half_texel_size.x, p.y + half_texel_size.y)).g,
        image.Sample(PointSampler, AF2(p.x + half_texel_size.x, p.y - half_texel_size.y)).g,
        image.Sample(PointSampler, AF2(p.x - half_texel_size.x, p.y - half_texel_size.y)).g
    );
#else
    return GatherGreen(LinearSampler, p, ASU2(0, 0));
#endif  
}

AF4 FsrEasuBF(AF2 p)
{
#ifdef GLSL
    // For GLSL we should use the textureGather function here, however that 
    // required GLSL 400+ while we are stuck with 330 for OBS. So we instead
    // mimic the functionality by using texel fetch.  
    //
    // For a given point p in normalized texture coordinates. We want to grab neighbouring
    // texels x, y, z, and w in the listed order.
    //
    //                                  +---+---+
    //                                  | w | z |
    //                                  +--(p)--+
    //                                  | x | y |
    //                                  +---+---+
    //
    // The size of a texel is pre-computed by the FsrEasuCon call and stored within the .xy
    // of con1. This is called on the CPU side and passed in via uniform to easu_const_1. 
    // To get the normalized texture coordinates for each required texel, we then offset 
    // point p by half the texel size using addition/subtraction as necessary.
    AF2 half_texel_size = AF2_(0.5) * easu_const_1.xy;
    return AF4(
        image.Sample(PointSampler, AF2(p.x - half_texel_size.x, p.y + half_texel_size.y)).b,
        image.Sample(PointSampler, AF2(p.x + half_texel_size.x, p.y + half_texel_size.y)).b,
        image.Sample(PointSampler, AF2(p.x + half_texel_size.x, p.y - half_texel_size.y)).b,
        image.Sample(PointSampler, AF2(p.x - half_texel_size.x, p.y - half_texel_size.y)).b
    );
#else
    return GatherBlue(LinearSampler, p, ASU2(0, 0));
#endif  
}

AF4 FsrRcasLoadF(ASU2 p)
{   
#ifdef GLSL
    return texelFetch(image, p, 0);
#else
    return image.Load(ASU3(p, 0));
#endif
}

void FsrRcasInputF(inout AF1 r, inout AF1 g, inout AF1 b)
{
    // NOT USED
}

#define FSR_RCAS_DENOISE
#include "ffx_fsr1_mod.h"

//=====================================================================================
//		VERTEX SHADER
//=====================================================================================

struct VSData 
{
    float4 pos : POSITION;
    float2 uv  : TEXCOORD0;
};

VSData VSMain(VSData vs_in)
{
    VSData vs_out;
    vs_out.pos = mul(float4(vs_in.pos.xyz, 1.0), ViewProj);
    vs_out.uv  = vs_in.uv;
    return vs_out;
}

//=====================================================================================
//		PIXEL SHADER
//=====================================================================================

float4 PSEASUMain(VSData vs_in) : TARGET
{
    AF3 out_col = AF3(0, 0, 0);
    AF2 out_pos = floor(vs_in.uv * output_size.xy); 

    FsrEasuF(out_col, out_pos, easu_const_0, easu_const_1, easu_const_2, easu_const_3);

    return float4(out_col, 1.0);
}

float4 PSRCASMain(VSData vs_in) : TARGET
{
    AF3 out_col = AF3(0, 0, 0);
    AF2 out_pos = floor(vs_in.uv * output_size.xy);
   
    FsrRcasF(out_col.r, out_col.g, out_col.b, out_pos, rcas_const_0);

    return float4(out_col, 1.0);
}

//=====================================================================================
//		TECHNIQUES
//=====================================================================================

technique EASU
{
    pass
    {
        vertex_shader = VSMain(vs_in);
        pixel_shader  = PSEASUMain(vs_in);
    }
};

technique RCAS
{
    pass
    {
        vertex_shader = VSMain(vs_in);
        pixel_shader  = PSRCASMain(vs_in);
    }
};